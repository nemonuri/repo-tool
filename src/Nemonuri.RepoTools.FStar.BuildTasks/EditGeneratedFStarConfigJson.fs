(**
### Reference

- [FStar/mk/test.mk](https://github.com/FStarLang/FStar/blob/master/mk/test.mk)
- [FStar/examples/fsharp.extraction.targets](https://github.com/FStarLang/FStar/blob/master/examples/fsharp.extraction.targets)
*)

// Note: 
// - verify, extract, build, run 각 Target 마다 공통적으로 사용되는 option들과, 
// - 개별적으로 사용되는 option들을 구분해야 하지 않을까?

namespace Nemonuri.RepoTools.FStar.BuildTasks

open Microsoft.Build.Framework
open System.IO
open FStarConfigJsonModelTheory
module M = MSBuildTheory

// Note: 
// - `FStarExe`, `Extra` 속성만 유지하고, 나머지는 변경
// - 시나리오를 위해서는, 다른 하위 태스크 또한 필요해!

type public EditGeneratedFStarConfigJson() =
    inherit Microsoft.Build.Utilities.Task()

    [<Required>]
    member val GeneratedFStarConfigJsonPath: string = "" with get,set

    member val Options: ITaskItem[] = [||] with get,set

    member val IncludeDirectories: ITaskItem[] = [||] with get,set

    override __.Execute(): bool =
        let logError a1 a2 a3 = M.logErrorAndFalse __ a1 a2 a3
        let gfcj = __.GeneratedFStarConfigJsonPath
        let gfcje = nameof __.GeneratedFStarConfigJsonPath

        try
            if System.String.IsNullOrWhiteSpace gfcj then
                logError "F* config json path required" gfcje gfcj
            else

            match
                File.ReadAllText gfcj
                |> parseGeneratedTextToModel
            with
                | NotJsonObject jn -> 
                    logError "Invalid Json format" (nameof System.Text.Json.JsonValueKind) (jn.GetValueKind())
                | JsonException e ->
                    M.logExceptionAndFalse __ e.SourceException
                | NotGeneratedFStarConfigJson jo ->
                    logError $"Not generated by {nameof GenerateFStarConfigJson}" (nameof System.Text.Json.Nodes.JsonObject) (jo.ToJsonString())
                | Success md ->

            let newMd = { 
                md with 
                    Options = Array.map M.getItemSpec __.Options
                    IncludeDirectories = Array.map M.getItemSpec __.IncludeDirectories }
            
            newMd 
            |> toJsonString
            |> fun v -> File.WriteAllText(gfcj, v)

            true
        with e ->
            M.logExceptionAndFalse __ e
